 {
        function* flattenContent(content) {
            if(Array.isArray(content)) {
                for(const item of content) {
                    if(Array.isArray(item)) {
                        for(const flat of flattenContent(item)) {
                            yield flat;
                        }
                    } else {
                        if(item.content) {
                            item.content = [...flattenContent(item.content)].reduce((array,item,i) => {
                                if(typeof(item)==="string" && typeof(array[i-1])==="string") {
                                    array[i-1] += item;
                                } else {
                                    array.push(item);
                                }
                                return array;
                            },[]);
                        }
                        yield item;
                    }
                }
            }
            return content;
        }
    }
    start
        = _ content:content* _ {
                return [...flattenContent(content)];
            }

    content
        = script / (expression _?) / stringcontent

    expression
        = tag:tag options:options? "[" _ content:content* _ "]"  {

            const {attributes, classes, id } = options||{},
                expression = {tag, content, id, attributes, classes};
            Object.defineProperty(expression,"location",{value:location()});
            return expression;
        }

    script "script"
        = "script" bracketed:bracketed { return {tag:"script", content:[bracketed.substring(1,bracketed.length-1)] } }

    bracketed "bracketed"
        = BRACKETOPEN text:(not_bracketed/bracketed)* BRACKETCLOSE { return "[" + text.join("") + "]" }

    not_bracketed
        = not:(!BRACKETCLOSE !BRACKETOPEN.) { return not.join("");}

    BRACKETOPEN = '['

    BRACKETCLOSE = ']'

    stringcontent
        = quoted / (start:nonreturnchars rest:(escapequoted / anything)* {return start + rest.join("")})

    escapequoted
        = string:quoted {return ' "' + string + '"'}

    nonreturnchars "nonreturn or tab chars"
        = !expression char:[ a-z0-9_\-~`!@#$%^&*()_+=\{\}|\\;:"'<,>.?/]i { return char; }

    anything "anything but bracket"
        = !expression char:[ a-z0-9_\-`~!@#$%^&*()_+=\{\}|\\;:"'<,>.?/\n\r\t]i  { return char; }

    tag
        = token

    options "options"
        = "(" _ id:id? _ attributes:attributelist? _ classes:class* _ ")" {
            var o = {};
            o.id=id;
            o.classList = classes;
            o.attributes = (attributes||[]).reduce((attributes,[name,value]) => { attributes[name] = value; return attributes; },{}); return o;
        }

    id "id"
        = "#" token:token { return token }

    class
        = "." cls:token { return cls }

    keyvaluelist "key value list"
        = first:keyvalue rest:( _ "," keyvalue)* { return [first,...rest.map((value) => value.pop())] }

    keyvalue "key value pair"
        = key:token _ ":" _ ["] value:[ a-z0-9_\-~`!@#$%^&*()_+=\[\]|\;'<>.?/]i* ["] { return [key,value.join("")]; }

    attributelist
        = attribute:(binaryattribute / attribute) _ attributelist:attributelist* { return [attribute].concat(...attributelist) }

    attribute
        =  name:token _ "=" _ value:quoted { return [name,value] }

    binaryattribute
        = hidden / checked / disabled / editable / fitcontent/ open / readonly / reversed / run / selected / static / toggle / url / visible

    hidden
        = "hidden" { return ["hidden",""] }

    checked
        = "checked" { return ["checked",""] }

    editable
        = "editable" { return ["editable",""] }

    disabled
        = "disabled" { return ["disabled",""] }

    fitcontent
        = "fitcontent" { return ["fitcontent",""] }

    readonly
        = "readonly" { return ["readonly",""] }

    reversed
        = "reversed" { return ["reversed",""] }

    run
        = "run" { return ["run",""] }

    open
        = "open" { return ["open",""] }

    selected
        = "selected" { return ["selected",""] }

    static
        = "static" { return ["static",""] }

    toggle
        = "toggle" { return ["toggle",""] }

    url "url"
        = prefix:urlprefix path:[a-z0-9_\-~`!@#$%^&*_+=\{\}\[\]|\\:;'<,>.?/]i* {
            try { new URL(path.join(),document.baseURI); return ["url",prefix+path.join("")]; }
            catch(e) { error(e+"") }
        }

    visible
        = "visible" { return ["visible",""] }

    urlprefix
        = "https://" / "./"

    token "token"
        = start:[a-z~!$%&*:@#]i rest:[a-z$%&*:@#0-9_\-]i* { return start + rest.join("") }

    quoted "quoted"
       = [\"] value:string [\"] { return value }

    string "string"
        = value:[ a-z0-9_\-~`!@#$%^&*()_+=\{\}\[\]|\\:;'<,>.?/]i* { return value.join("") }

    _ "whitespace"
        = whitespace:[ \t\n\r]* { return whitespace.join("") }